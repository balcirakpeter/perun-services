#!/usr/bin/perl
use strict;
use warnings;
no if $] >= 5.017011, warnings => 'experimental::smartmatch';
use Net::LDAPS;
use Net::LDAP::Entry;
use Net::LDAP::Message;
use Net::LDAP::LDIF;

# Import shared AD library
use ADConnector;
use ScriptLock;

sub process_add;
sub process_remove;
sub process_update;

# log counters
my $counter_add = 0;
my $counter_remove = 0;
my $counter_update = 0;
my $counter_fail = 0;
my $counter_skipped = 0;

# define service
my $service_name = "ad_group_mu_ucn";

# GEN folder location
my $facility_name = $ARGV[0];
chomp($facility_name);
my $service_files_base_dir="../gen/spool";
my $service_files_dir="$service_files_base_dir/$facility_name/$service_name";

# GROUP BASE DN
open my $group_file, '<', "$service_files_dir/baseDNGroups";
my $group_base_dn = <$group_file>;
chomp($group_base_dn);
close $group_file;

# USER BASE DN
open my $user_file, '<', "$service_files_dir/baseDNUsers";
my $user_base_dn = <$user_file>;
chomp($user_base_dn);
close $user_file;

# propagation destination
my $namespace = $ARGV[1];
chomp($namespace);

# create service lock
my $lock = ScriptLock->new($facility_name . "_" . $service_name . "_" . $namespace);
($lock->lock() == 1) or die "Unable to get lock, service propagation was already running.";

# init configuration
my @conf = init_config($namespace);
my @ldap_locations = resolve_domain_controlers($conf[0]);
my $ldap = ldap_connect_multiple_options(\@ldap_locations);
my $groupFilter = '(objectClass=group)';
my $userFilter = '(objectClass=person)';

# connect
ldap_bind($ldap, $conf[1], $conf[2]);

# load all data
my @perun_group_entries = load_perun($service_files_dir . "/" . $service_name . ".ldif");
my @ad_group_entries = load_ad($ldap, $group_base_dn, $groupFilter, ['cn']);
my @ad_user_entries = load_ad($ldap, $user_base_dn, $userFilter, ['dn']);

my %ad_user_entries_map = ();
my %ad_group_entries_map = ();
my %perun_group_entries_map = ();

foreach (@ad_user_entries) {
        my $dn = $_->get_value('dn');
        $ad_user_entries_map{ $dn } = $_;
}
foreach my $ad_entry (@ad_group_entries) {
	my $cn = $ad_entry->get_value('cn');
	$ad_group_entries_map{ $cn } = $ad_entry;
}
foreach my $perun_entry (@perun_group_entries) {
	my $cn = $perun_entry->get_value('cn');
	$perun_group_entries_map{ $cn } = $perun_entry;
}

# process data
process_add();
process_remove();
process_update();

# disconnect
ldap_unbind($ldap);

# log results
ldap_log($service_name, "Added: " . $counter_add . " entries.");
ldap_log($service_name, "Removed: " . $counter_remove . " entries.");
ldap_log($service_name, "Updated: " . $counter_update. " entries.");
ldap_log($service_name, "Failed: " . $counter_fail. " entries.");
ldap_log($service_name, "Contains skipped members: " . $counter_skipped. " entries.");

# print results for TaskResults in GUI
print "Added: " . $counter_add . " entries.\n";
print "Removed: " . $counter_remove . " entries.\n";
print "Updated: " . $counter_update. " entries.\n";
print "Failed: " . $counter_fail. " entries.\n";
print "Contains skipped members: " . $counter_fail. " entries.\n";

$lock->unlock();

if ($counter_fail > 0) { die "Failed to process: " . $counter_fail . " entries.\nSee log at: ~/perun-engine/send/logs/$service_name.log";}

# END of main script

###########################################
#
# Main processing functions
#
###########################################

#
# Add new group entries to AD
#
sub process_add() {

	foreach my $perun_entry (@perun_group_entries) {

		my $cn = $perun_entry->get_value('cn');
		unless (exists $ad_group_entries_map{$cn}) {

			#FIXME optimize work with memory
			my $perun_entry_copy = $perun_entry->clone;
			$perun_entry_copy->delete('member');

			# Add new entry to AD
			my $response = $perun_entry_copy->update($ldap);
			unless ($response->is_error()) {
				# SUCCESS
				ldap_log($service_name, "Added: " . $perun_entry_copy->dn());
				$counter_add++;
			} else {
				# FAIL
				ldap_log($service_name, "NOT added: " . $perun_entry_copy->dn() . " | " . $response->error());
				$counter_fail++;
			}

		}
	}

}

#
# Remove group entries in AD
#
sub process_remove() {

	foreach my $ad_entry (@ad_group_entries) {
		my $cn = $ad_entry->get_value('cn');
		unless (exists $perun_group_entries_map{$cn}) {

			my $response = $ldap->delete($ad_entry);
			unless ($response->is_error()) {
				ldap_log($service_name, "Deleted entry: " . $ad_entry->dn());
				$counter_remove++;
			} else {
				ldap_log($service_name, "NOT deleted: " . $ad_entry->dn() . " | " . $response->error());
				$counter_fail++;
			}

		}
	}

}

#
# Update group members in AD
#
sub process_update() {

	foreach my $perun_entry (@perun_group_entries) {

		my @per_val = $perun_entry->get_value('member');

		# load members of a group from AD based on DN in Perun => Group must exists in AD
		my @ad_val = load_group_members($ldap, $perun_entry->dn(), $groupFilter);

		if ($? != 0) {
			ldap_log($service_name, "Unable to load Perun group members from AD: " . $perun_entry->dn());
			next;
		}

		# sort to compare
		my @sorted_ad_val = sort(@ad_val);
		my @sorted_per_val = sort(@per_val);

		# compare using smart-match (perl 5.10.1+)
		unless(@sorted_ad_val ~~ @sorted_per_val) {

			my %ad_val_map = map { $_ => 1 } @sorted_ad_val;
			my %per_val_map = map { $_ => 1 } @sorted_per_val;

			my @to_be_added;
			my @to_be_removed;
			my @not_exist;

			# add members
			foreach my $per_val_member (@sorted_per_val) {
				unless (exists $ad_val_map{$per_val_member}) {
					if (exists $ad_user_entries_map{$per_val_member}) {
						push (@to_be_added, $per_val_member);
					} else {
						push (@not_exist, $per_val_member);
					}
				}
			}

			# remove members
			foreach my $ad_val_member (@sorted_ad_val) {
				unless (exists $per_val_map{$ad_val_member}) {
					push (@to_be_removed, $ad_val_member);
				}
			}

			# members of group are not equals
			# we must get reference to real group from AD in order to call "update"
			my $response_ad = $ldap->search( base => $perun_entry->dn(), filter => $groupFilter, scope => 'base' );
			unless ($response_ad->is_error()) {
				# SUCCESS

				my $ad_entry = $response_ad->entry(0);

				if (@to_be_added) {
					$ad_entry->add(
						'member' => \@to_be_added
					);
				}

				if (@to_be_removed) {
					$ad_entry->delete(
						'member' => \@to_be_removed
					);
				}

				if (@to_be_added || @to_be_removed) {
					# Update entry in AD
					my $response = $ad_entry->update($ldap);

					if ($response) {
						unless ($response->is_error()) {
							# SUCCESS (group updated)
							$counter_update++;
							ldap_log($service_name, "Group members added: " . $ad_entry->dn() . " | \n" . join(",\n", @to_be_added));
							ldap_log($service_name, "Group members removed: " . $ad_entry->dn() . " | \n" . join(",\n",@to_be_removed));
						} else {
							# FAIL (to update group)
							$counter_fail++;
							ldap_log($service_name, "Group members NOT updated: " . $ad_entry->dn() . " | " . $response->error());
							ldap_log($service_name, "Group members who should be added: " . $ad_entry->dn() . " | \n" . join(",\n", @to_be_added));
							ldap_log($service_name, "Group members who should be removed: " . $ad_entry->dn() . " | \n" . join(",\n",@to_be_removed));
						}
					}
				}
				if (@not_exist) {
					$counter_skipped++;
					ldap_log($service_name, "Group members skipped: " . $ad_entry->dn() . " | These members do not exist in AD.\n" . join(",\n", @not_exist));
				}

			} else {
				# FAIL (to get group from AD)
				$counter_fail++;
				ldap_log($service_name, "Group members NOT updated: " . $perun_entry->dn() . " | " . $response_ad->error());
			}
		}

		# group is unchanged

	}

}
